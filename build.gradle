buildscript {
  repositories {
    mavenCentral()
    maven {
      name = 'jFrogPlugins'
      url = 'http://dl.bintray.com/jfrog/jfrog-jars'
    }
  }

  dependencies {
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.0.0"
  }
}

allprojects {
  apply plugin: 'com.jfrog.artifactory-publish'

  ext {
    isSnapshot = version.contains('SNAPSHOT')
  }

  repositories {
    mavenCentral()
  }

  artifactory {
    contextUrl = 'http://oss.jfrog.org'
  }
}

subprojects {
  apply plugin: 'groovy'
  apply plugin: "maven"
  apply plugin: 'maven-publish'
  apply plugin: "signing"

  dependencies {
    compile (
      gradleApi(),
      localGroovy()
    )

    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
      exclude group:'org.codehaus.groovy'
    }
  }

  test {
    testLogging {
      exceptionFormat "full"
      if(project.hasProperty('stdOut')) {
        showStandardStreams true
      }
    }
  }

  artifactoryPublish { task ->
    rootProject.artifactory {
      publish {
        repository {
          repoKey = isSnapshot ? 'oss-snapshot-local' : 'oss-release-local'

          // Need this to enable non deployment builds without credentials
          gradle.taskGraph.whenReady { taskGraph ->
            if (taskGraph.hasTask(task)) {
              username = bintrayUser
              password = bintrayKey
            }
          }
        }
      }
    }
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
      }
    }
  }

}

artifactoryPublish {
  skip true
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.0'
}
