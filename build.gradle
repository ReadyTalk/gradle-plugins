buildscript {
  repositories {
    mavenCentral()
    mavenRepo( name: 'jFrogPlugins', url: 'http://dl.bintray.com/jfrog/jfrog-jars' )
  }

  dependencies {
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:2.1.0"
  }
}

allprojects {
  apply plugin: 'artifactory'

  group = "com.readytalk"
  version = "0.2.0-SNAPSHOT"

  ext {
    isSnapshot = version.contains('SNAPSHOT')
  }

  repositories {
    mavenCentral()
  }

  artifactory {
    contextUrl = 'http://oss.jfrog.org'
  }
}

subprojects {
  apply plugin: 'groovy' 
  apply plugin: "maven"
  apply plugin: 'maven-publish'
  apply plugin: "signing"

  dependencies {
    compile (
      gradleApi(),
      localGroovy()
    )

    testCompile (
      'org.spockframework:spock-core:0.6-groovy-1.8'
    )
  }

  test.testLogging {
    exceptionFormat "full"
    if(project.hasProperty('stdOut')) {
      showStandardStreams true
    }
  }

  artifactoryPublish { task ->
    rootProject.artifactory {
      publish {
        repository {
          repoKey = isSnapshot ? 'oss-snapshot-local' : 'oss-release-local'

          // Need this to enable non deployment builds without credentials
          gradle.taskGraph.whenReady { taskGraph ->
            if (taskGraph.hasTask(task)) {
              username = bintrayUser
              password = bintrayKey
            }
          }
        }
      }
    }
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
      }
    }
  }

}

artifactoryPublish {
  skip true
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.6'
}
